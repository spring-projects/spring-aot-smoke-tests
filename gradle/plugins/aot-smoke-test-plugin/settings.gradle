pluginManagement {
	new File(rootDir.parentFile.parentFile.parentFile, "gradle.properties").withInputStream {
		def properties = new Properties()
		properties.load(it)
		properties.each { key, value ->
			if (key.endsWith("Version")) {
				gradle.extensions.extraProperties.set(key, value)
			}
		}
	}
	plugins {
		id "io.spring.javaformat" version gradle.extensions.extraProperties.get("javaFormatVersion")
	}
	repositories {
		if (System.env['REPO_SPRING_IO_USERNAME'] != null && System.env['REPO_SPRING_IO_PASSWORD'] != null) {
			maven {
				name = "Maven Central Mirror"
				url = "https://repo.spring.io/artifactory/repo1"
				credentials {
					username = System.env['REPO_SPRING_IO_USERNAME']
					password = System.env['REPO_SPRING_IO_PASSWORD']
				}
				mavenContent {
					releasesOnly()
				}
			}
		}
		else {
			mavenCentral()
		}
	}
}

rootProject.name="aot-smoke-test-plugin"
