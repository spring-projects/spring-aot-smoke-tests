pluginManagement {
	includeBuild "gradle/plugins/aot-smoke-test-plugin"
	repositories {
		if (System.env['REPO_SPRING_IO_USERNAME'] != null && System.env['REPO_SPRING_IO_PASSWORD'] != null) {
			maven {
				name = "Maven Central Mirror"
				url = "https://repo.spring.io/artifactory/repo1"
				credentials {
					username = System.env['REPO_SPRING_IO_USERNAME']
					password = System.env['REPO_SPRING_IO_PASSWORD']
				}
				mavenContent {
					releasesOnly()
				}
			}
		}
		else {
			mavenCentral()
		}
		gradlePluginPortal()
		maven {
			name = "Spring Snapshot"
			url = "https://repo.spring.io/artifactory/snapshot"
			mavenContent {
				snapshotsOnly()
			}
		}
		maven {
			name = "Spring Milestone"
			url = "https://repo.spring.io/artifactory/milestone"
			mavenContent {
				releasesOnly()
			}
		}
	}
}

plugins {
	id "com.gradle.develocity" version "3.19.2"
	id "io.spring.develocity.conventions" version "0.0.22"
}

rootProject.name="spring-aot-smoke-tests"

include "aot-smoke-test-support"

["batch", "boot", "cloud", "data", "framework", "graphql", "integration", "security", "session"].each { group ->
	file(group).eachDirMatch(~/[a-z].*/) { smokeTest ->
		include "$group:${smokeTest.name}"
	}
}

develocity {
	buildScan {
		value("Spring Boot generation", "${springBootGeneration}")
	}
}